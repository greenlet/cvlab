apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

Properties properties = new Properties()
def propertiesFile = project.rootProject.file('local.properties')
properties.load(propertiesFile.newDataInputStream())
def eigen3Path = properties.getProperty('eigen3.path').toString()
def ceresPath = properties.getProperty('ceres.path').toString()
def opencvPath = properties.getProperty('opencv.path').toString()
def prodKeystorePassword = properties.getProperty('prod.keystore.password').toString()
println "Eigen3 path: ${eigen3Path}"
println "Ceres path: ${ceresPath}"
println "OpenCV path: ${opencvPath}"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        applicationId "ai.cvlab"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_static"
            }
        }
    }

    signingConfigs {
        release {
            storeFile file("../../../keys/cvlab-android-prod.keystore")
            storePassword prodKeystorePassword
            keyAlias "cvlab_release"
            keyPassword prodKeystorePassword
        }
        stage {
            storeFile file("../../../keys/cvlab-android-prod.keystore")
            storePassword prodKeystorePassword
            keyAlias "cvlab_stage"
            keyPassword prodKeystorePassword
        }
        debug {
            storeFile file("../../../keys/cvlab-android-dev.keystore")
            storePassword "cvlab_debug"
            keyAlias "cvlab_debug"
            keyPassword "cvlab_debug"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    arguments "-DOPENCV_SDK_PATH=${opencvPath}/Release/sdk"
                }
            }
            signingConfig signingConfigs.release
        }
        stage {
            applicationIdSuffix ".stage"
//            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    arguments "-DEIGEN3_PATH=${eigen3Path}/Release",
                            "-DCERES_PATH=${ceresPath}/Release",
                            "-DOPENCV_SDK_PATH=${opencvPath}/Release/sdk",
                            "-DCMAKE_BUILD_TYPE=Release"
                }
            }
            signingConfig signingConfigs.stage
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            externalNativeBuild {
                cmake {
                    arguments "-DEIGEN3_PATH=${eigen3Path}/Release",
                            "-DCERES_PATH=${ceresPath}/Release",
                            "-DOPENCV_SDK_PATH=${opencvPath}/Release/sdk",
                            "-DCMAKE_BUILD_TYPE=Release"

                }
            }
            signingConfig signingConfigs.debug
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    def camerax_version = "1.0.0-beta08"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha15"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    def nav_version = "2.3.1"

//    // Java language implementation
//    implementation "androidx.navigation:navigation-fragment:$nav_version"
//    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    // Jetpack Compose Integration
    implementation "androidx.navigation:navigation-compose:1.0.0-alpha01"

}
